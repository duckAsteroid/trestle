plugins {
    id "com.jfrog.bintray" version "1.7.3"
    id "org.sonarqube" version "2.5"
}

/* Java basics */
apply plugin: 'java'
group = 'com.asteroid.duck'
version = '1.0.0'
description = 'A mocking framework for REST APIs'

sourceCompatibility = 1.9

// workaround for sonarqube plugin issue in Gradle 4
//sourceSets.main.output.classesDir = new File(buildDir, "classes/main")

repositories {
    mavenCentral()
    mavenLocal()
}

dependencies {
    compile 'org.slf4j:slf4j-api:1.7.25'
    compile 'org.nanohttpd:nanohttpd:2.3.1'
    compile 'commons-io:commons-io:2.6'
    compile 'org.glassfish.jersey.core:jersey-client:2.26'

    testCompile 'junit:junit:4.+'
    testCompile 'org.json:json:20171018'

    testRuntime 'org.slf4j:slf4j-simple:1.7.25'
    testRuntime 'org.glassfish.jersey.media:jersey-media-json-jackson:2.26'
    testRuntime 'org.glassfish.jersey.core:jersey-common:2.26'
    testRuntime 'org.glassfish.jersey.inject:jersey-hk2:2.26'
    testRuntime group: 'javax.activation', name: 'activation', version: '1.1.1'


}

// configure sonar (using secure ENV var from travis)
sonarqube {
    properties {
        property "sonar.organization", "duckasteroid-github"
        property "sonar.host.url", "https://sonarcloud.io"
        property "sonar.login", "$System.env.SONAR_LOGIN"
    }
}

/* Add ability to publish JARs to Maven (e.g. bintray) */
apply plugin: 'maven-publish'
publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
}


/* Configuration to publish from travis to bintray */
bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    publications = ['mavenJava'] // reference above publication
    publish = true // Whether version should be auto published after an upload    
    override = true // Whether to override version artifacts already published
    pkg {
        name = 'basic-template' // < - EDIT ME
        repo = 'maven'
        userOrg = 'duck-asteroid'
        licenses = ['MIT']
        vcsUrl = 'https://github.com/duckAsteroid/basic-template.git' // < - EDIT ME
    }
}